apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "kotlin-kapt"
apply plugin: 'androidx.navigation.safeargs'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.example.forecast"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.incremental": "true"]
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }
}
androidExtensions {
    experimental = true
}
dependencies {
    def app_compat = '1.1.0'
    implementation fileTree(dir: 'libs', include: ['*.jar']) // 文件结构树
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version" // 科特林基础包
    implementation "androidx.appcompat:appcompat:$app_compat" // 安卓兼容
    implementation "androidx.preference:preference:1.1.1" // 偏好设置
    implementation "com.google.android.material:material:$app_compat"  // 材料设计风格
    implementation "androidx.core:core-ktx:1.2.0" // 科特林安卓扩展
    implementation "androidx.constraintlayout:constraintlayout:1.1.3" // 约束布局
    implementation "com.google.code.gson:gson:2.8.6"  // 谷歌Json解析
    implementation "com.google.android.gms:play-services-location:17.0.0"  // 谷歌定位
    implementation "androidx.legacy:legacy-support-v4:1.0.0"    // 安卓v4包迁移
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.4'  // ThreeTen(反向端口)世界时间
    implementation 'androidx.multidex:multidex:2.0.1' // 方法数溢出修复

    // 布局导航面板
    def navigation = '2.2.2'
    implementation "androidx.navigation:navigation-fragment:$navigation"
    implementation "androidx.navigation:navigation-ui:$navigation"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation"

    // 视图模型与生命周期
    def lifecycle = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle"
    kapt "androidx.lifecycle:lifecycle-common-java8:$lifecycle"

    // Room(房间) 数据库
    def room = "2.2.5"
    implementation "androidx.room:room-runtime:$room"
    kapt "androidx.room:room-compiler:$room"

    // 科特林安卓协程
    def coroutines = '1.3.5-2-native-mt-1.4-M1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines"

    // Retrofit(改造) 网络解析
    def retrofit = "2.8.1"
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2"

    // 科特林依赖注入
    def di = "5.2.0"
    implementation "org.kodein.di:kodein-di-generic-jvm:$di"
    implementation "org.kodein.di:kodein-di-framework-android-x:$di"

    // Glide(滑翔) 图像加载
    def glide = "4.11.0"
    implementation "com.github.bumptech.glide:glide:$glide"
    kapt "com.github.bumptech.glide:compiler:$glide"

    // Groupie(追星族) 适配器
    def groupie = "2.3.0"
    implementation "com.xwray:groupie:$groupie"
    implementation "com.xwray:groupie-kotlin-android-extensions:$groupie"

    // 项目单元测试
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
